function(compute_curl DL_URL BASENAME EXTENSION UNPACK_DIR_OUT DL_FILENAME_OUT)
	set(DOWNLOADS_DIR "${PROJECT_BINARY_DIR}/Downloads")
	get_filename_component(DL_FILENAME "${DL_URL}" NAME)

	#if the URL has the correct extension, make the unpack directory and dl_filename match it
	string(REGEX MATCH "${EXTENSION}$" EXTENSION_TEST "${DL_URL}")
	if(NOT EXTENSION_TEST STREQUAL "")
		string(LENGTH ${EXTENSION} EXT_LEN)
		string(LENGTH "${DL_FILENAME}" FILENAME_LEN)		
		math(EXPR BASENAME_LEN "${FILENAME_LEN} - ${EXT_LEN}")
		string(SUBSTRING "${DL_FILENAME}" 0 ${BASENAME_LEN} DL_BASENAME)
		set(${UNPACK_DIR_OUT} "${DOWNLOADS_DIR}/${DL_BASENAME}" PARENT_SCOPE)
		set(${DL_FILENAME_OUT} "${DL_BASENAME}${EXTENSION}" PARENT_SCOPE)
	else()
		set(${UNPACK_DIR_OUT} "${DOWNLOADS_DIR}/${BASENAME}" PARENT_SCOPE)
		set(${DL_FILENAME_OUT} "${BASENAME}${EXTENSION}" PARENT_SCOPE)
	endif()
endfunction()

function(curl_and_unpack DL_URL BASENAME EXTENSION)
	set(DOWNLOADS_DIR "${PROJECT_BINARY_DIR}/Downloads")	
	compute_curl("${DL_URL}" "${BASENAME}" "${EXTENSION}" UNPACK_DIR DL_FILENAME)

	if (NOT EXISTS ${UNPACK_DIR})
		set(DL_LOCATION "${DOWNLOADS_DIR}/${DL_FILENAME}")
		if (NOT EXISTS "${DL_LOCATION}")
			message(STATUS "Downloading ${DL_URL} to ${DL_LOCATION}")
			file(MAKE_DIRECTORY "${DOWNLOADS_DIR}")
			file(DOWNLOAD "${DL_URL}" "${DL_LOCATION}")
		endif()
		set(TEMP_DIR "${DOWNLOADS_DIR}/5f320be2-0824-41d6-9dd9-51ff16787c27")
		file(REMOVE_RECURSE "${TEMP_DIR}")
		file(MAKE_DIRECTORY "${TEMP_DIR}")
		message(STATUS "Unpacking ${DL_FILENAME} to ${TEMP_DIR}")
		execute_process(COMMAND "${CMAKE_COMMAND}" "-E" tar xf "${DL_LOCATION}" WORKING_DIRECTORY "${TEMP_DIR}")
		file(GLOB TOP_ENTRIES "${TEMP_DIR}/*")
		list(LENGTH TOP_ENTRIES TOP_ENTRIES_COUNT)
		if(${TOP_ENTRIES_COUNT} EQUAL "1" AND IS_DIRECTORY "${TOP_ENTRIES}")
			message(STATUS "Moving ${TOP_ENTRIES} to ${UNPACK_DIR}")
			file(RENAME "${TOP_ENTRIES}" "${UNPACK_DIR}")	
			file(REMOVE_RECURSE "${TEMP_DIR}")		
		else()
			message(STATUS "Moving ${TEMP_DIR} to ${UNPACK_DIR}")
			file(RENAME "${TEMP_DIR}" "${UNPACK_DIR}")
		endif()
	endif()
endfunction()
