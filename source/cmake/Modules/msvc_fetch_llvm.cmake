#This file will download the LLVM sources for MSVC++ builds

function(msvc_fetch_llvm)
	if(${MSVC})
		set(LLVM_INSTALL_DIR "${PROJECT_BINARY_DIR}\\LLVM")
		set(LLVM_CONFIG_FILE "${LLVM_INSTALL_DIR}\\share\\llvm\\cmake\\LLVMConfig.cmake")
		if (NOT EXISTS "${LLVM_CONFIG_FILE}")
			if(${LLVM_VERSION} STREQUAL "")
				message(WARNING "cannot prepare LLVM without valid version specified")
			else()
				file(MAKE_DIRECTORY "${LLVM_INSTALL_DIR}")
				set(DOWNLOADS_DIR "$ENV{USERPROFILE}\\Downloads")
				set(LLVM_DL_SITE "http://llvm.org/releases/${LLVM_VERSION}")
				set(LLVM_DL_BASENAME "llvm-${LLVM_VERSION}.src")
				set(LLVM_DL_FILENAME "${LLVM_DL_BASENAME}.tar.xz")
				set(LLVM_DL_URI "${LLVM_DL_SITE}/${LLVM_DL_FILENAME}")
				set(LLVM_DL_LOCATION "${DOWNLOADS_DIR}\\${LLVM_DL_FILENAME}")
				if(NOT EXISTS "${LLVM_DL_LOCATION}")
					message(STATUS "Downloading ${LLVM_DL_URI} to ${LLVM_DL_LOCATION}")
					file(DOWNLOAD "${LLVM_DL_URI}" "${LLVM_DL_LOCATION}")
				endif()
				set(LLVM_SOURCE_DIR "${DOWNLOADS_DIR}\\${LLVM_DL_BASENAME}")
				if(NOT EXISTS "${LLVM_SOURCE_DIR}\\CMakeLists.txt")
					message(STATUS "Unpacking ${LLVM_DL_LOCATION}")
					execute_process(COMMAND "${CMAKE_COMMAND}" "-E" tar xf "${LLVM_DL_LOCATION}" WORKING_DIRECTORY "${DOWNLOADS_DIR}")
				endif()
				set(LLVM_BINARY_DIR "${LLVM_SOURCE_DIR}\\build")
				file(MAKE_DIRECTORY "${LLVM_BINARY_DIR}")
				if(NOT EXISTS "${LLVM_BINARY_DIR}\\LLVM.sln")
					message(STATUS "Configuring LLVM-${LLVM_VERSION}")
					execute_process(COMMAND "${CMAKE_COMMAND}" "${LLVM_SOURCE_DIR}" "-G" "${CMAKE_GENERATOR}" "-DCMAKE_INSTALL_PREFIX=${LLVM_INSTALL_DIR}" WORKING_DIRECTORY "${LLVM_BINARY_DIR}")
				endif()
				message(STATUS "Building LLVM")
				execute_process(COMMAND "${CMAKE_COMMAND}" "--build" "${LLVM_BINARY_DIR}")
				message(STATUS "Installing LLVM to ${LLVM_INSTALL_DIR}")
				execute_process(COMMAND "${CMAKE_COMMAND}" "--build" "${LLVM_BINARY_DIR}" "--target" "INSTALL")
			endif()
		endif()
		include("${LLVM_CONFIG_FILE}")
		set(LLVM_INCLUDE_DIRS "${LLVM_INCLUDE_DIRS}" PARENT_SCOPE)
		set(LLVM_DEFINITIONS "${LLVM_DEFINITIONS}" PARENT_SCOPE)
	endif()
endfunction()
	
msvc_fetch_llvm()