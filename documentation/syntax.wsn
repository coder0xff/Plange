#creates an algebraic value storing the simultaneous results of addition and subtraction
ADD_SUB = EXPRESSION {IC} ("±"|"+-") {IC} EXPRESSION.

ADDITION = EXPRESSION {IC} "+" {IC} EXPRESSION.

#constrain ALL elements of a Collection to be true
ALL = "∀" {IC} EXPRESSION.

#create an object on the heap
ALLOCATION = "alloc" {IC} INVOCATION.

AND = EXPRESSION {IC} ("∧"|"and") {IC} EXPRESSION.

ARRAY = "[" {IC} [EXPRESSION {{IC} "," {IC} EXPRESSION} {IC} ] "]".

#place the given value into the given variable
ASSIGNMENT = EXPRESSION {IC} ("←"|"<-") {IC} {EXPRESSION {IC} ("←"|"<-") {IC}} EXPRESSION.

#a bijection exists between the left and right hand operands
BIJECTION = EXPRESSION {IC} ("↔"|"<->") {IC} EXPRESSION.

#the collection of binary operators that work on numbers (among other things)
BINARY_ARITHMETIC_OP = ADDITION | SUBTRACTION | ADD_SUB | MULTIPLICATIVE_OP | DIVISION | INTEGER_DIVISION | EXPONENTIATION | MODULATION | SHIFTL | SHIFTR.

BINARY_LOGICAL_OP = AND | OR | XOR | IMPLICATION | IFF | MAPS_TO | BIJECTION.

BINARY_OP = BINARY_ARITHMETIC_OP | BITWISE_OP | COLLECTION_OP | CONSTRUCTIVE_OP | BINARY_LOGICAL_OP | RELATIONAL_OP.

BIT_AND = EXPRESSION {IC} "&&" {IC} EXPRESSION.

BIT_OR = EXPRESSION {IC} "||" {IC} EXPRESSION.

BIT_XOR = EXPRESSION {IC} "^^" {IC} EXPRESSION.

BITWISE_OP = BIT_AND | BIT_OR | BIT_XOR.

#the two ordinals of the Bool type
BOOL = "true" | "false".

CARDINALITY = "#" {IC} EXPRESSION.

#change the type of the specified expression to the parenthesized type
CAST = PARENTHETICAL {IC} EXPRESSION.

CEILING = ("⌈" {IC} EXPRESSION {IC} "⌉") | ( "|'" {IC} EXPRESSION {IC} "'|" ).

#splits the left hand side collection into a collection of collections each the size of the right hand side
CHUNK = EXPRESSION {IC} "/" {IC} EXPRESSION.

#returns a collection containing the elements that would not be consumed from the end of the left hand side by taking the chunk of the right hand side
CHUNK_MODULO = EXPRESSION {IC} "%" {IC} EXPRESSION.

#the set construction and collections operators
COLLECTION_OP = CHUNK | CHUNK_MODULO | DIFFERENCE | INTERSECTION | SYMMETRIC_DIFFERENCE | UNION.

#an inline comment
COMMENT = "/*" {all} "*/".

#function composition operator
COMPOSITION = EXPRESSION {IC} ("∘"|"_o_") {IC} EXPRESSION.

#ternary conditional
CONDITIONAL = EXPRESSION {IC} "?" {IC} EXPRESSION {IC} ":" {IC} EXPRESSION.

#operators that are constructive for making values 
CONSTRUCTIVE_OP = COMPOSITION | PREPEND | NULL_COALESCE.

#scalar multiplication or Vector<3, N> cross product
CROSS_PRODUCT = EXPRESSION {IC} ("×"|"_x_") {IC} EXPRESSION.

#declare a variable, see: statement
DECLARATION = [XML_DOC_STRING {IC}] ["extern" {IC}] (TYPE_DEREFERENCE | VOLATILE_TYPE_DEREFERENCE) {IC} IDENTIFIER.

#declare a variable and assign a value to it, see: statement
DECLARATION_ASSIGNMENT = [XML_DOC_STRING {IC}] [ (TYPE_DEREFERENCE | IMPLICIT_TYPE_DEREFERENCE | VOLATILE_IMPLICIT_TYPE_DEREFERENCE | VOLATILE_TYPE_DEREFERENCE) {IC} ] IDENTIFIER {IC} ("←"|"<-") {IC} EXPRESSION.

DELTA = ("Δ"|"_delta_") ["^" WHOLE_NUMBER] expression

#define a constant
DEFINITION = [XML_DOC_STRING {IC}] [ (TYPE_DEREFERENCE | IMPLICIT_TYPE_DEREFERENCE) {IC} ] IDENTIFIER {IC} ":=" {IC} EXPRESSION.

#collection difference (similar to set difference)
DIFFERENCE = EXPRESSION {IC} "/" {IC} EXPRESSION.

#a dimension used in dimensional analysis
DIMENSION = IDENTIFIER | DIMENSIONAL_ANALYSIS_OP.

#operators in a dimension
DIMENSIONAL_ANALYSIS_OP = UNIT_MULTIPLICATION | UNIT_DIVISION | UNIT_EXPONENTIATION.

#a number with a trailing dimension
DIMENSIONAL_NUMBER = NUMBER DIMENSION.

DIVISION = EXPRESSION {IC} ("÷"|"/" ) {IC} EXPRESSION.

#a do while/until loop
DO = "do" {IC} EXPRESSION [ {IC} ("while"|"until") {IC} PARENTHETICAL ].

#scalar multiplication or Vector<D, N> inner product
DOT_PRODUCT = EXPRESSION {IC} ("⋅"|"_dot_") {IC} EXPRESSION.

#a comment bounded by arbirtrarily-sized delimiters
EMBEDDED_COMMENT = "/*" EMBEDDED_COMMENT_INTERIOR "*/".

EMBEDDED_COMMENT_INTERIOR = PAYLOAD | "*" EMBEDDED_COMMENT_INTERIOR "*".

#a string bounded by arbirtrarily-sized delimiters
EMBEDDED_STRING = ("\"\"\"\n" {all} "\n\"\"\"") | ("\"\"\"" {all} "\"\"\"") | ("\"" EMBEDDED_STRING "\"").

END_OF_LINE_COMMENT = "//" {all} "\n".

EQUALITY = EXPRESSION {IC} "=" {IC} EXPRESSION.

#a constraint asserting that at least one element of the specified Collection are true
EXISTS = "∃" {IC} EXPRESSION.

#a constraint asserting that exactly one element of the specified Collection are true
EXISTS_ONE = "∃!" {IC} EXPRESSION.

EXPONENTIATION = EXPRESSION {IC} "^" {IC} EXPRESSION.

#a linearization of an evaluation tree containing constants, variables, operators, invocations, and sub EXPRESSIONs
EXPRESSION = ALLOCATION | BINARY_OP | BOOL | c_string | CAST | CEILING | CONDITIONAL | DELTA | DIMENSIONAL_NUMBER | EMBEDDED_STRING | FACTORIAL | FLOOR | FUNCTION | SET | IDENTIFIER | INVOCATION | LIST | MAGNITUDE | MAP | MEMBER_ACCESS | NEAREST_INTEGER | NEGATION | NUMBER | PARENTHETICAL | RADICAL | RANGE | SET_COMPREHENSION | TUPLE | TYPE | UNARY_OP | VECTOR_NORM | "null" | "this" | "this_type".

FACTORIAL = EXPRESSION {IC} "!".

FLOOR = ( "⌊" {IC} EXPRESSION {IC} "⌋" ) | ( "|_" {IC} EXPRESSION {IC} "_|" ).

#evaluate the second expression repeatedly while the second expression is true, all after evaluating the optional STATEMENT_SCOPE
FOR = "for" {IC} "(" {IC} [ STATEMENT_SCOPE {IC} ] EXPRESSION {IC} ";" {IC} [ (ASSIGNMENT | EXPRESSION) {IC} ] ")" {IC} EXPRESSION.

#evaluate the last expression repeatedly while iterating over the items of the specified Collection
FOR_COLLECTION = "for" {IC} "(" {IC} (([ (TYPE_DEREFERENCE | IMPLICIT_TYPE_DEREFERENCE) {IC} ] IDENTIFIER ) | EXPRESSION ) {IC} ("in"|"∈") {IC} EXPRESSION {IC} ")" {IC} EXPRESSION.

#a number with a decimal point
FRACTIONAL = (decimal_digit {decimal_digit} "." {decimal_digit}) | ("." decimal_digit {decimal_digit}).

FREE = "free" {IC} EXPRESSION.

#create a function
FUNCTION = [ "(" {IC} [ PARAMETER {{IC} "," {IC} PARAMETER} {IC} ] ")" {IC} ] [("stable"|"unstable") {IC} ] ["no_throw" {IC} ] ["atomic" {IC} ] {PLATFORM {IC}} [("pure"|"imperative"|"opaque") {IC} ] "{" STATEMENT_SCOPE "}".

GREATER_THAN = EXPRESSION {IC} ">" {IC} EXPRESSION.

#the in operator, backward
HAS = EXPRESSION {IC} ("∋"|"has") {IC} EXPRESSION.

#a hexidecimal number
HEX = "0x" hexadecimal_digit {hexadecimal_digit}.

#intermediary content - regions that whitespaces and comments can go
IC = COMMENT | EMBEDDED_COMMENT | END_OF_LINE_COMMENT | WS.

#the name of a variable or constant 
IDENTIFIER = (letter | "_") {letter | number | "_"}.

#returns the expression corresponding to the first parenthetical that evaluates to true when coerced to a boolean, or the else case if none of them evaluate to true, assuming that the else case is present, otherwise returning void
IF = "if" {IC} PARENTHETICAL {IC} EXPRESSION {{IC} "elif" {IC} PARENTHETICAL {IC} EXPRESSION} [ {IC} "else" {IC} EXPRESSION ].

#semantic equivalence
IFF = EXPRESSION {IC} ("⇔"|"<=>") {IC} EXPRESSION.

IMPLICATION = EXPRESSION {IC} ("⇒"|"=>") {IC} EXPRESSION.

#use the type inferred on the right hand side
IMPLICIT_TYPE_DEREFERENCE = "<" {IC} ">".

#Collection membership
IN = EXPRESSION {IC} ("∈"|"in") {IC} EXPRESSION.

INEQUALITY = EXPRESSION {IC} ("≠"|"/=") {IC} EXPRESSION.

#see: type
INHERITANCE_LIST=[VISIBILITY_MODIFIER {IC}] EXPRESSION {{IC} "," {IC} [VISIBILITY_MODIFIER {IC}] EXPRESSION}.

INTEGER = (NON_ZERO_DECIMAL_DIGIT {decimal_digit}) | "0".

#same as ⌊x/y⌋
INTEGER_DIVISION = EXPRESSION {IC} "\\" {IC} EXPRESSION.

#Collection intersection
INTERSECTION = EXPRESSION {IC} "∩" {IC} EXPRESSION.

#invoke a function, type-function or array-function
INVOCATION = EXPRESSION(( "(" {IC} [ EXPRESSION {{IC} "," {IC} EXPRESSION} {IC} ] ")" ) | ( "<" {IC} [ EXPRESSION {{IC} "," {IC} EXPRESSION} {IC} ] ">" ) | ( "[" {IC} [ EXPRESSION {{IC} "," {IC} EXPRESSION} {IC} ] "]" ) ).

IMPORT = "import" {IC} EXPRESSION.

#the free monoid operator
KLEENE_STAR = EXPRESSION {IC} "*".

LESSER_THAN = EXPRESSION {IC} "<" {IC} EXPRESSION.

#a Collection of items
LIST = "[|" {IC} [EXPRESSION {{IC} ", " {IC} EXPRESSION} {IC} ] "|]".

#evaluate an expression repeatedly while or until a parenthetical evaluates to true
LOOP = ( "while" | "until" ) {IC} PARENTHETICAL {IC} EXPRESSION.

#absolute value, vector magnitude, cardinality
MAGNITUDE = "|" {IC} EXPRESSION {IC} "|".

#an associative Collection
MAP = "{" {IC} EXPRESSION {IC} ":" {IC} EXPRESSION {{IC} "," {IC} EXPRESSION {IC} ":" {IC} EXPRESSION} {IC} "}".

#function type builder
MAPS_TO = EXPRESSION {IC} ("→"|"->") {IC} EXPRESSION.

MEMBER_ACCESS = EXPRESSION {IC} "." {IC} IDENTIFIER.

#mod operator
MODULATION = EXPRESSION {IC} "%" {IC} EXPRESSION.

#scalar multiplication, cartesian product
MULTIPLICATION = EXPRESSION {IC} "*" {IC} EXPRESSION.

MULTIPLICATIVE_OP = MULTIPLICATION | DOT_PRODUCT | CROSS_PRODUCT.

#round to the nearest integer
NEAREST_INTEGER = ( "⌊" {IC} EXPRESSION {IC} "⌉" ) | ("|_" {IC} EXPRESSION {IC} "'|" ).

NEGATION = "-" {IC} EXPRESSION.

#used for constructing number
NON_ZERO_DECIMAL_DIGIT = "1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9".

#boolean not
NOT = ("¬"|"~") {IC} EXPRESSION.

NOT_GREATER_THAN = EXPRESSION {IC} ("≤"|"<=") {IC} EXPRESSION.

#not_in, backward
NOT_HAS = EXPRESSION {IC} ("∌"|"/has") {IC} EXPRESSION.

#Collection anti-membership
NOT_IN = EXPRESSION {IC} ("∉"|"/in") {IC} EXPRESSION.

NOT_LESSER_THAN = EXPRESSION {IC} ("≥"|">=") {IC} EXPRESSION.

#return the first expression if non-null otherwise return the second expression
NULL_COALESCE = EXPRESSION {IC} "??" {IC} EXPRESSION.

NUMBER = INTEGER | FRACTIONAL | HEX | OCTAL.

#octal number
OCTAL = "0" octal_digit {octal_digit}.

OR = EXPRESSION {IC} ("∨"|"or") {IC} EXPRESSION.

#declares a formal argument of a function
PARAMETER = ([ TYPE_DEREFERENCE {IC} ] IDENTIFIER) | ([ (TYPE_DEREFERENCE | IMPLICIT_TYPE_DEREFERENCE) {IC} ] IDENTIFIER {IC} "=" {IC} EXPRESSION | EXPRESSION [ {IC} "=" {IC} EXPRESSION ]).

#an expression in parenthesis
PARENTHETICAL = "(" {IC} EXPRESSION {IC} ")".

#used in embedded_string
PAYLOAD = {all}.

#see: function
PLATFORM = "_" {letter | number | "_"}.

POST_DEC = EXPRESSION {IC} "--".

POST_INC = EXPRESSION {IC} "++".

PRE_DEC = "--" {IC} EXPRESSION.

PRE_INC = "++" {IC} EXPRESSION.

#create a new Collection starting with the left hand side followed by the elements of the right hand side Collection
PREPEND = EXPRESSION {IC} "&" {IC} EXPRESSION.

PROPER_SUBSET = EXPRESSION {IC} ("⊂"|"psubs") {IC} EXPRESSION.

PROPER_SUPERSET = EXPRESSION {IC} ("⊃"|"psups") {IC} EXPRESSION.

RADICAL = "√" {IC} EXPRESSION.

#create a totally-ordered open/closed/half-open discreet/continuous Collection
RANGE = "(" {IC} "..." {IC} EXPRESSION {IC} ("]"|")") | ("["|"(") {IC} EXPRESSION {IC} "..." {IC} ")" | ("("|"[") {IC} EXPRESSION {IC} "..." {IC} EXPRESSION {IC} ("]"|")").

#relational operators on collections
RELATIONAL_COLLECTION_OP = SUBSET | PROPER_SUBSET | SUPERSET | PROPER_SUPERSET | IN | NOT_IN | HAS | NOT_HAS.

RELATIONAL_OP = EQUALITY | INEQUALITY | LESSER_THAN | GREATER_THAN | NOT_GREATER_THAN | NOT_LESSER_THAN | RELATIONAL_COLLECTION_OP.

#exit the current function, optionally returning a value
RETURN = "return" [ {IC} EXPRESSION ].

SET = "{|" {IC} [EXPRESSION {{IC} "," {IC} EXPRESSION} {IC}] "|}".

#create a Collection, substituting variables bound in the right hand side into the left hand side
SET_COMPREHENSION = "{" {IC} EXPRESSION {IC} "|" {IC} EXPRESSION {IC} "}".

#shift left
SHIFTL = EXPRESSION {IC} "<<" {IC} EXPRESSION.

#shift right
SHIFTR = EXPRESSION {IC} ">>" {IC} EXPRESSION.

#top-level statement syntax
STATEMENT = ( ASSIGNMENT | DECLARATION | DECLARATION_ASSIGNMENT | DEFINITION | DO | EXPRESSION | FOR | FOR_COLLECTION | IF | IMPORT | LOOP | RETURN | THROW | TRY | "break" | "continue" ) {IC} ";".

#zero or more statements
STATEMENT_SCOPE = {IC | STATEMENT}.

SUBSET = EXPRESSION {IC} ("⊆"|"subs") {IC} EXPRESSION.

SUBTRACTION = EXPRESSION {IC} "-" {IC} EXPRESSION.

SUPERSET = EXPRESSION {IC} ("⊇"|"sups") {IC} EXPRESSION.

#the xor of Collections
SYMMETRIC_DIFFERENCE = EXPRESSION {IC} "⊖" {IC} EXPRESSION.

#raise an exception
THROW = "throw" [{IC} EXPRESSION].

#executes the specified nullary void function of the first expression, catching exceptions that pattern match the function(s) of the second expression, and then executes the third expression unconditionally
TRY = "try" {IC} EXPRESSION {IC} "catch" {IC} EXPRESSION {"catch" {IC} EXPRESSION} [{IC} "finally" {IC} EXPRESSION].

#create a finite ordered list of elements
TUPLE = "(" {IC} EXPRESSION {{IC} ", " {IC} EXPRESSION} {IC} ")".

#create a type
TYPE = "type" {IC} ["inheriting" {IC} INHERITANCE_LIST {IC}] ["implementing" {IC} INHERITANCE_LIST {IC}] "{" TYPE_SCOPE "}".

#use a variable/constant as a typename
TYPE_DEREFERENCE = "<" {IC} EXPRESSION {IC} ">".

#zero or more statements or member declarations/definitions
TYPE_SCOPE = {IC} {TYPE_STATEMENT {IC}}.

#top-level type syntax
TYPE_STATEMENT = (VISIBILITY_MODIFIER {IC} ":") | (["static" {IC}] (DECLARATION_ASSIGNMENT | DECLARATION ) {IC} "at" {IC} WHOLE_NUMBER) | STATEMENT.

UNARY_ARITHMETIC_OP = FACTORIAL | NEGATION | POST_DEC | POST_INC | PRE_DEC | PRE_INC | RADICAL.

UNARY_LOGICAL_OP = NOT | ALL | EXISTS | EXISTS_ONE.

UNARY_OP = CARDINALITY | KLEENE_STAR | UNARY_ARITHMETIC_OP | UNARY_LOGICAL_OP.

#Collection union
UNION = EXPRESSION {IC} "∪" {IC} EXPRESSION.

#dimensional analysis "per"
UNIT_DIVISION = DIMENSION "/" DIMENSION.

#dimensional analysis square units, cubed units, etc
UNIT_EXPONENTIATION = DIMENSION "^" INTEGER.

#dimensional analysis "by"
UNIT_MULTIPLICATION = DIMENSION "*" DIMENSION.

#create a Vector<D, N> normalized to unit length
VECTOR_NORM = "||" {IC} EXPRESSION {IC} "||".

VISIBILITY_MODIFIER = ("public"|"protected"|"protected internal"|"internal"|"private").

VOLATILE_IMPLICIT_TYPE_DEREFERENCE = "<" {IC} "volatile" {IC} ">".

VOLATILE_TYPE_DEREFERENCE = "<" {IC} "volatile" {IC} EXPRESSION {IC} ">".

WHOLE_NUMBER = ("1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9") {"0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"}.

WS = white_space {white_space}.

#documentation string bounded by arbirtrarily-sized delimiters
XML_DOC_STRING=("```" {all} "```") | ("`" XML_DOC_STRING "`").

XOR = EXPRESSION {IC} ("⊕" |"xor") {IC} EXPRESSION.
